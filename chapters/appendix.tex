\chapter{Congruence Rules} \label{app:cong}

\input{figures/congruence.tex}

\iffalse
The following two sections contain the full congruence rules for
the congruent closure of reduction in \lang,
and for equivalence in \CICE.
The shorthand rules \rref*{red-cong} and \rref*{equiv-cong} can then be proven
as theorems by induction on the respective congruence rules.
\fi

\section{Congruence Rules for Reduction in \lang} \label{app:cong:red}

\FigCongRed{fig:cong:red*}
\FigCongRedCase{fig:cong:red*-case}

\section{Congruence Rules for Complete Development in \lang} \label{app:cong:develop}

\FigCongTakahashi{fig:cong:develop}
\vfill % don't touch!

\section{Congruence Rules for Equivalence in \CICE} \label{app:cong:equiv}

\FigCongEquiv{fig:cong:equiv}

\chapter{Mechanized \CICE Definitions} \label{app:mechanization}

\setmonofont{iosevka.ttc}

\section{Coq}

The following type check on Coq 8.15.2.

\subsection{Preliminary definitions} \label{app:mechanization:coq:prelim}

\begin{singlespace}
\begin{minted}{coq}
From Equations Require Import Equations.
Require Import Coq.Program.Equality.
Require Import Coq.Unicode.Utf8_core.

Reserved Notation "r ≤ s" (at level 70, no associativity).

Inductive Size : Type :=
| suc : Size → Size
| lim : ∀ {A : Type}, (A → Size) → Size.

Inductive Leq : Size → Size → Type :=
| mono : ∀ {r s}, r ≤ s → suc r ≤ suc s
| cocone : ∀ {s A f} (a : A), s ≤ f a → s ≤ lim f
| limiting : ∀ {s A f}, (∀ (a : A), f a ≤ s) → lim f ≤ s
where "r ≤ s" := (Leq r s).

Definition Lt r s : Type := suc r ≤ s.
Notation "r < s" := (Lt r s).

Definition base : Size := lim (False_rect Size).

Definition baseLeq s : base ≤ s :=
  limiting (λ a, (False_rect (_ ≤ s) a)).

Fixpoint reflLeq {s} : s ≤ s :=
  match s with
  | suc s => mono reflLeq
  | lim f => limiting (λ a, cocone a reflLeq)
  end.

Property transLeq {r s t} (rs : r ≤ s) (st : s ≤ t) : r ≤ t.
Admitted.

(*
Derive NoConfusion for Size.
Equations transLeq {r s t : Size} (rs : r ≤ s) (st : s ≤ t) : r ≤ t :=
  transLeq (mono rs) (mono st) := mono (transLeq rs st);
  transLeq rs (cocone a sfa) := cocone a (transLeq rs sfa);
  transLeq (limiting fas) st := limiting (λ a, transLeq (fas a) st);
  transLeq (cocone a rfa) (limiting fat) := transLeq rfa (fat a).
*)

Fixpoint sucLeq s : s ≤ suc s :=
  match s with
  | suc s => mono (sucLeq s)
  | lim f => limiting (λ a, transLeq (sucLeq (f a)) (mono (cocone a reflLeq)))
  end.

Inductive Acc (s : Size) : Prop :=
acc { accLt : ∀ r, r < s → Acc r }.
Arguments accLt {_}.

Axiom funext : ∀ {A} {B : A → Type} {p q : ∀ x, B x},
  (∀ x, p x = q x) → p = q.

Equations accIsProp {s} (acc1 acc2 : Acc s) : acc1 = acc2 :=
| acc _ p, acc _ q =>
  f_equal _ (funext (λ r, funext (λ rs, accIsProp (p r rs) (q r rs)))).

Lemma accLeq : ∀ r s, r ≤ s → Acc s → Acc r.
Proof.
  intros r s rs acc.
  induction acc as [s p IH].
  exact (acc r (λ t tr, p t (transLeq tr rs))).
Qed.

Theorem wf : ∀ s, Acc s.
Proof.
  intros s.
  induction s as [s IH | A f IH].
  - refine (acc (suc s) (λ r rsucs, accLeq r s _ IH)).
    inversion rsucs as [r' s' rs | |].
    exact rs.
  - refine (acc (lim f) (λ r rlimf, _)).
    inversion rlimf as [| r' A' f' a rfa eqr eqA |].
    dependent destruction H.
    destruct (IH a) as [p].
    exact (p r rfa).
Qed.

Definition wfInd P IH s : P s :=
  let wfInd := fix wfAcc s acc {struct acc} :=
    IH s (λ r rs, wfAcc r (acc.(accLt) r rs))
  in wfInd s (wf s).
\end{minted}
\end{singlespace}

\subsection{W Types} \label{app:mechanization:coq:W}

\begin{singlespace}
\begin{minted}{coq}
Import SigTNotations.

Inductive W (A : Type) (B : A → Type) (s : Size) : Type :=
| sup : ∀ r, r < s → ∀ a, (B a → W A B r) → W A B s.
Arguments sup {_ _ _}.

Definition ac {A B} a (f : B a → {s & W A B s}) : {s & B a → W A B s} :=
  let f' b :=
    match (f b).2 with
    | sup r rs a f => sup r (transLeq rs (cocone b reflLeq)) a f
    end
  in (lim (λ b, (f b).1) ; f').
\end{minted}
\end{singlespace}

\subsection{Full Inductives and Infinite Sizes}

\begin{singlespace}
\begin{minted}{coq}
(* Naturals *)

Inductive N (s : Size) : Type :=
| zero : ∀ r, r < s → N s
| succ : ∀ r, r < s → N r → N s.
Arguments zero {_}.
Arguments succ {_}.

Equations limN (n : nat) : Size :=
| O => suc base
| S n => suc (limN n).

Equations natToN (n : nat) : N (limN n) :=
| O => zero base reflLeq
| S n => succ (limN n) reflLeq (natToN n).

Definition infN : Size := lim limN.
Definition leqInfN (n : nat) : limN n ≤ infN := cocone n reflLeq.

(* W types *)

Inductive WInf (A : Type) (B : A → Type) : Type :=
| sup' : ∀ a, (B a → WInf A B) → WInf A B.

Equations limW {A} {B} (w : WInf A B) : Size :=
| sup' a f => suc (lim (λ b, limW (f b))).

Equations liftW {A} {B} {r} {s} : r ≤ s → W A B r → W A B s :=
| rs, sup t tr a f => sup t (transLeq tr rs) a f.

Equations WInfToW {A} {B} (w : WInf A B) : W A B (limW w) :=
| sup' a f => sup (lim (λ b, limW (f b))) reflLeq a (λ b, liftW _ (WInfToW (f b))).

Definition infW {A} {B} : Size := lim (@limW A B).
Definition leqInfW {A} {B} (w : WInf A B) : limW w ≤ @infW A B := cocone w reflLeq.
\end{minted}
\end{singlespace}

\section{Agda}

The following type check on Agda 2.6.2.

\subsection{Preliminary definitions} \label{app:mechanization:agda:prelim}

\begin{singlespace}
\begin{minted}{agda}
{-# OPTIONS --without-K #-}

open import Agda.Primitive using (Level; lsuc)
open import Relation.Binary.PropositionalEquality.Core using (_≡_; cong)
open import Data.Empty using (⊥; ⊥-elim)

variable
  ℓ ℓ′ : Level
  A C : Set ℓ
  B : A → Set ℓ

infix 30 ↑_
infix 30 ⊔_

data Size {ℓ} : Set (lsuc ℓ) where
  ↑_ : Size {ℓ} → Size
  ⊔_ : {A : Set ℓ} → (A → Size {ℓ}) → Size

data _≤_ {ℓ} : Size {ℓ} → Size {ℓ} → Set (lsuc ℓ) where
  ↑s≤↑s : ∀ {r s} → r ≤ s → ↑ r ≤ ↑ s
  s≤⊔f  : ∀ {s} f (a : A) → s ≤ f a → s ≤ ⊔ f
  ⊔f≤s  : ∀ {s} f → (∀ (a : A) → f a ≤ s) → ⊔ f ≤ s

◯ : Size
◯ = ⊔ ⊥-elim

◯≤s : ∀ {s} → ◯ ≤ s
◯≤s = ⊔f≤s ⊥-elim λ ()

s≤s : ∀ {s : Size {ℓ}} → s ≤ s
s≤s {s = ↑ s} = ↑s≤↑s s≤s
s≤s {s = ⊔ f} = ⊔f≤s f (λ a → s≤⊔f f a s≤s)

s≤s≤s : ∀ {r s t : Size {ℓ}} → r ≤ s → s ≤ t → r ≤ t
s≤s≤s (↑s≤↑s r≤s) (↑s≤↑s s≤t) = ↑s≤↑s (s≤s≤s r≤s s≤t)
s≤s≤s r≤s (s≤⊔f f a s≤fa) = s≤⊔f f a (s≤s≤s r≤s s≤fa)
s≤s≤s (⊔f≤s f fa≤s) s≤t = ⊔f≤s f (λ a → s≤s≤s (fa≤s a) s≤t)
s≤s≤s (s≤⊔f f a s≤fa) (⊔f≤s f fa≤t) = s≤s≤s s≤fa (fa≤t a)

s≤↑s : ∀ {s : Size {ℓ}} → s ≤ ↑ s
s≤↑s {s = ↑ s} = ↑s≤↑s s≤↑s
s≤↑s {s = ⊔ f} = ⊔f≤s f (λ a → s≤s≤s s≤↑s (↑s≤↑s (s≤⊔f f a s≤s)))

_<_ : Size {ℓ} → Size {ℓ} → Set (lsuc ℓ)
r < s = ↑ r ≤ s

record Acc (s : Size {ℓ}) : Set (lsuc ℓ) where
  inductive
  pattern
  constructor acc
  field acc< : (∀ r → r < s → Acc r)
open Acc

accIsProp : ∀ {s : Size {ℓ}} → (acc1 acc2 : Acc s) → acc1 ≡ acc2
accIsProp (acc p) (acc q) =
  cong acc (funext p q (λ r → funext (p r) (q r) (λ r<s → accIsProp (p r r<s) (q r r<s))))
  where postulate funext : ∀ (p q : ∀ x → B x) → (∀ x → p x ≡ q x) → p ≡ q

acc≤ : ∀ {r s : Size {ℓ}} → r ≤ s → Acc s → Acc r
acc≤ r≤s (acc p) = acc (λ t t<r → p t (s≤s≤s t<r r≤s))

wf : ∀ (s : Size {ℓ}) → Acc s
wf (↑ s) = acc (λ { _ (↑s≤↑s r≤s) → acc≤ r≤s (wf s) })
wf (⊔ f) = acc (λ { r (s≤⊔f f a r<fa) → (wf (f a)).acc< r r<fa })

wfInd : ∀ (P : Size {ℓ} → Set ℓ′) → (∀ s → (∀ r → r < s → P r) → P s) → ∀ s → P s
wfInd P f s = wfAcc s (wf s)
  where wfAcc : ∀ s → Acc s → P s
        wfAcc s (acc p) = f s (λ r r<s → wfAcc r (p r r<s))
\end{minted}
\end{singlespace}

\subsection{W Types} \label{app:mechanization:agda:W}

\begin{singlespace}
\begin{minted}{agda}
open import Data.Product using (proj₁; proj₂; Σ-syntax; _,_)

data W (A : Set ℓ) (B : A → Set ℓ) (s : Size {ℓ}) : Set (lsuc ℓ) where
  sup : ∀ r → r < s → (a : A) → (B a → W A B r) → W A B s

ac : ∀ a → (B a → Σ[ s ∈ Size ] W A B s) → Σ[ s ∈ Size ] (B a → W A B s)
ac a f = ⊔ (λ b → proj₁ (f b)) , f′
  where f′ : _
        f′ b with proj₂ (f b)
        ... | sup r r<s a f = sup r (s≤s≤s r<s (s≤⊔f _ b s≤s)) a f
\end{minted}
\end{singlespace}

\subsection{Full Inductives and Infinite Sizes}

\begin{singlespace}
\begin{minted}{agda}
open import Agda.Builtin.Nat using (Nat; zero; suc)

{- Naturals -}
-- Sized naturals
data N (s : Size) : Set (lsuc lzero) where
  zero : ∀ r → r < s → N s
  succ : ∀ r → r < s → N r → N s

-- The size of any given full natural
limN : Nat → Size
limN zero = ↑ ◯
limN (suc n) = ↑ limN n

-- Producing a sized natural from a full natural
Nat→N : (n : Nat) → N (limN n)
Nat→N zero = zero ◯ s≤s
Nat→N (suc n) = succ (limN n) s≤s (Nat→N n)

-- The infinite size for naturals
∞ᶰ : Size
∞ᶰ = ⊔ limN

-- The infinite size is greater or equal to the size of any natural
≤∞ᶰ : ∀ n → limN n ≤ ∞ᶰ
≤∞ᶰ n = s≤⊔f limN n s≤s

{- W types -}
-- Full W types
data W∞ (A : Set ℓ) (B : A → Set ℓ) : Set ℓ where
  sup∞ : ∀ a → (B a → W∞ A B) → W∞ A B

-- The size of any given full W type
limW : W∞ A B → Size
limW (sup∞ a f) = ↑ ⊔ λ b → limW (f b)

-- Producing a sized W type from a full W type
W∞→W : (w : W∞ A B) → W A B (limW w)
W∞→W (sup∞ a f) =
  sup (⊔ λ b → limW (f b)) s≤s a (λ b → liftW (s≤⊔f (limW ∘ f) b s≤s) (W∞→W (f b)))
  where liftW : ∀ {r s} → r ≤ s → W A B r → W A B s
        liftW r≤s (sup t t≤r a f) = sup t (s≤s≤s t≤r r≤s) a f

-- The infinite size for a given W type
∞ʷ : ∀ {A : Set ℓ} {B : A → Set ℓ} → Size
∞ʷ {B = B} = ⊔ limW {B = B}

-- The infinite size is greater or equal to the size of any W type
≤∞ʷ : ∀ (w : W∞ A B) → limW w ≤ ∞ʷ {A = A} {B = B}
≤∞ʷ w = s≤⊔f limW w s≤s
\end{minted}
\end{singlespace}